buildscript {
    dependencies {
        classpath "de.guntram.mcmod:crowdin-translate:${project.crowdin_translate_version}"
    }
    repositories {
        maven {
            name = "CrowdinTranslate"
            url = "https://minecraft.guntram.de/maven/"
        }
    }
}

plugins {
    id "fabric-loom" version "0.11-SNAPSHOT"
    id "org.ajoberstar.grgit" version "4.1.0"
}

apply plugin: "de.guntram.mcmod.crowdin-translate"
crowdintranslate.crowdinProjectname = "sync-fabric"
crowdintranslate.minecraftProjectName = "sync"
crowdintranslate.verbose = true

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

String loaderDependencyVersion = project.loader_version.split(/\+/)[0]
String minecraftDependencyVersion = project.minecraft_version.split(/[+ -]/)[0]

group = project.maven_group
version = "${project.mod_version}${getVersionMetadata()}" as Object
archivesBaseName = "${project.archives_base_name}-mc${minecraftDependencyVersion}"

loom {
    accessWidenerPath = file("src/main/resources/sync.accesswidener")
}

repositories {
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        name = "Ladysnake Libs"
        url = "https://ladysnake.jfrog.io/artifactory/mods"
    }
    maven {
        name = "Cloth Config"
        url = "https://maven.shedaniel.me/"
        content {
            includeGroup "me.shedaniel.cloth"
        }
    }
    maven {
        name = "Nexus OSS Repository"
        url = "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = "CrowdinTranslate"
        url = "https://minecraft.guntram.de/maven/"
    }
    maven {
        name = "JitPack"
        url = "https://jitpack.io"
        content {
            includeGroupByRegex "(io|com)\\.github\\..*"
        }
    }
    maven {
        url = "https://maven.jamieswhiteshirt.com/libs-release/"
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }
    maven {
        url = "https://maven.cafeteria.dev"
        content {
            includeGroup "net.adriantodt.fabricmc"
        }
    }
    mavenLocal()
}

// To change the versions see the gradle.properties file
dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation(include("de.guntram.mcmod:crowdin-translate:${project.crowdin_translate_version}"))
    modImplementation(include("teamreborn:energy:${project.tr_energy_version}"))
    modImplementation(include("dev.kir:smart-recipes:${project.smartrecipes_version}"))
    modImplementation(include("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}"), {
        exclude group: "net.fabricmc.fabric-api"
    })

    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    modImplementation "dev.emi:trinkets:${project.trinkets_version}"
    modImplementation "io.github.ladysnake:requiem-api:${project.requiem_version}"
    modImplementation "com.github.apace100:origins-fabric:${project.origins_version}"
}

processResources {
    def fabricModProperties = [
        "version": version,
        "fabric": ">=${loaderDependencyVersion}",
        "minecraft": ">=${minecraftDependencyVersion}",
        "smart_recipes": project.smartrecipes_version,
        "cloth_config": project.cloth_version,
        "crowdin_translate": project.crowdin_translate_version,
        "team_reborn_energy": project.tr_energy_version,
        "modmenu": ">=${project.modmenu_version}",
        "trinkets": ">=${project.trinkets_version}",
        "requiem": ">=${project.requiem_version}",
        "origins": ">=${project.origins_version}",
    ]

    filesMatching("fabric.mod.json") {
        expand fabricModProperties
    }
}

tasks.withType(JavaCompile).configureEach {
    // Ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    it.options.release.set(17)
}

java {
    withSourcesJar()
}

jar {
    from "LICENSE.md"
}

@SuppressWarnings('GrMethodMayBeStatic') // It cannot
String getVersionMetadata() {
    // Release builds don't require additional metadata
    String release_tag = System.getenv("RELEASE_TAG")
    if (release_tag != null && release_tag.length() != 0) {
        return ""
    }

    // GitHub CI builds only
    String build_id = System.getenv("GITHUB_RUN_NUMBER")
    if (build_id != null && build_id.length() != 0) {
        return "+build.${build_id}"
    }

    if (grgit != null) {
        def head = grgit.head()
        def id = head.abbreviatedId

        // Flag the build if the build tree is not clean
        if (!grgit.status().clean) {
            id += "-dirty"
        }

        return "+rev.${id}"
    }

    // No tracking information could be found about the build
    return "+unknown"
}
